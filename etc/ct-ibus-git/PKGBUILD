# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=ct-ibus-git
pkgname=(ct-ibus-git ct-libibus-git)
pkgver=r2612.52b7272d
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="https://github.com/ibus/ibus"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'python-dbus' 'python-gobject' 'iso-codes' 'python2-gobject2' 'python2-dbus' 'python2-gobject' 'librsvg')
makedepends=('gobject-introspection' 'vala' 'intltool' 'git' 'gnome-common' 'gtk-doc' 'gconf' 'qt5-base' 'nodejs-emojione')
conflicts=('ibus' 'libibus')
options=('!emptydirs')
_gitname="ibus"
source=("git+https://github.com/ibus/ibus.git")

pkgver() {
    cd "$srcdir/${_gitname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${_gitname}
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/ibus \
        --sysconfdir=/etc \
        --disable-gconf \
        --enable-dconf \
        --enable-wayland \
        --enable-gtk-doc \
        --disable-memconf \
        --enable-ui \
        --enable-python-library \
        --with-python=python3
    make
}

package_ct-ibus-git() {
    depends+=("ct-libibus-git=$pkgver")
    install=ibus.install

    cd ${_gitname}
    make DESTDIR="${pkgdir}" install
    make -C src DESTDIR="${pkgdir}" uninstall
    make -C bindings DESTDIR="${pkgdir}" uninstall
    rm -r "$pkgdir/usr/lib/python2.7/site-packages/gi" # compiled pyc/pyo files were not cleaned
    make DESTDIR="${pkgdir}" uninstall-pkgconfigDATA
}

package_ct-libibus-git() {
    pkgdesc="IBus support library"
    depends=('glib2')

    cd ${_gitname}
    make -C src DESTDIR="${pkgdir}" install
    make -C bindings DESTDIR="${pkgdir}" install
    make DESTDIR="${pkgdir}" install-pkgconfigDATA
}

sha512sums=('SKIP')
